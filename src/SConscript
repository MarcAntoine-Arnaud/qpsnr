
Import( 'env' )
Import( 'envPy' )

ffmpegDependencies = [
	"avutil",
	"avcodec",
	"avformat",
	"swscale",
]

qpsnrLib = env.SharedLibrary(
	'qpsnr',
	Glob( '*.cpp' ),
	LIBS = ffmpegDependencies,
)

qpsnrLibStatic = env.StaticLibrary(
	'qpsnr',
	Glob( '*.cpp' ),
	LIBS = ffmpegDependencies,
)

pythonQpsnr = envPy.SharedLibrary(
	'qpsnr',
	source='qpsnr.i',
	LIBS=[
		qpsnrLib,
		"python"
	] + ffmpegDependencies
)

Command("__init__.py", None, [ Touch("$TARGET"), ])

env.Alias( 'install', env.InstallVersionedLib( 'lib', qpsnrLib ) )
env.Alias( 'install', env.Install( 'lib', qpsnrLibStatic ) )

env.Alias( "install", env.Install( "include" , 'qpsnr.hpp' ) )
env.Alias( "install", env.Install( "lib/python2.7/site-packages/qpsnr", pythonQpsnr ) )
env.Alias( "install", env.Install( "lib/python2.7/site-packages/qpsnr", Glob( 'qpsnr.py' ) ) )
env.Alias( "install", env.Install( "lib/python2.7/site-packages/qpsnr", "__init__.py" ) )

Export( 'qpsnrLib' )
Export( 'qpsnrLibStatic' )
